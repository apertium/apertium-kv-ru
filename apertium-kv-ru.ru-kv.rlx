DELIMITERS = "<.>" "<!>" "<?>" ; 
SOFT-DELIMITERS = "<,>" ;

###############################################################################

# Tags

LIST N = n ;
LIST A = adj (vblex pp) ;
LIST V = vblex ;
LIST Adv = adv ;
LIST Num = num ;
LIST Pr = pr ;
LIST Pron = prn ;
LIST Det = det ;
LIST CC = cnjcoo ;
LIST CS = cnjsub ;
LIST Interj = ij ;

LIST Interr = itg ; 

LIST Msc = m ;
LIST Fem = f ;
LIST Neu = nt ;
LIST MFN = mfn ;

LIST Nom = nom ;
LIST Gen = gen ;
LIST Dat = dat ;
LIST Acc = acc ;
LIST Ins = ins ;
LIST Prp = prp ;

# Tag groups 

LIST NGDAIP = nom gen dat acc ins prp ;
LIST GENDER = m f nt mf mfn ;
LIST NUMBER = sg pl sp ;
LIST ANIMAC = aa nn an ;

# Lexicalised lists 

LIST NOMPREP = "за" "в" ;
LIST GENPREP = "без" "против" "напротив" "кроме" "вместо" "у" "близ" 
               "вдоль" "возле" "мимо" "около" "среди" "посреди" "вокруг" 
               "из" "с" "от" "после" "из-за" "ис-под" "вне" "внутри" 
               "для" "ради" "до" "накануне" ;
LIST DATPREP = "по" "к" "вопреки" "наперекор" "благодаря" "согласно" ;
LIST ACCPREP = "в" "с" "на" "про" "за" "о" "под" "сквозь" "по" "через" ;
LIST PRPPREP = "в" "на" ;
LIST INSPREP = "с" "над" "под" "между" "перед" "за" ;

LIST ACCINSPREP =  "с" "под" "за" ;

LIST PosIndecl = ("её" det pos) ("его" det pos) ("их" det pos) ;

LIST ODIN = "один" "1" ;
LIST DVA = "два" "2" ;
LIST TRI = "три" "3" ;

# Sets 

SET Pers = (p1) | (p2) | (p3) ;

SET NounMscFem = (n m) OR (n f) OR (n mfn) ;
SET NounMscNeu = (n m) OR (n nt) OR (n mfn) ;
SET NounFemNeu = (n f) OR (n nt) OR (n mfn) ;

SET NOTMASC = Fem | Neu ;
SET NOTFEM = Msc | Neu ;
SET NOTNEUT = Msc | Fem | (mf) ;

SET NOTNOM = Gen | Dat | Acc | Ins | Prp ;
SET NOTGEN = Nom | Dat | Acc | Ins | Prp ;
SET NOTDAT = Nom | Gen | Acc | Ins | Prp ;
SET NOTACC = Nom | Gen | Dat | Ins | Prp ;
SET NOTINS = Nom | Gen | Dat | Acc | Prp ;
SET NOTPRP = Nom | Gen | Dat | Acc | Ins ;

SET NOTACCINS = Nom | Gen | Dat | Prp ; 

SET OBL    = NGDAIP - Nom ;

# Boundary sets 

SET WORD = N | V | A | Pr | Pron | Det | Adv | CC | CS | Interj | Num | ("\?") ;

SET PRE-N =  A | Det | (n gen) | Num | (prn gen) | CC ; # Det???

LIST COMMA = (",") ;

SET MARK =  COMMA | ("\\") | ("\;") ; #"

SET WORDMARK = WORD | MARK ;

SET NPNH = WORDMARK - PRE-N ;
SET NPNHA = WORDMARK - PRE-N - Adv ;
SET NOT-ADV = WORDMARK - Adv ;

SET NP-MEMBER = PRE-N | N | Pron ;

SET S-BOUNDARY = CS | Interr | (";") ;

SECTION

SELECT Det IF (0 PosIndecl) (0 Pers) ((1C N) OR (1C PRE-N LINK *1C N BARRIER NPNHA));
	## Практика показывает, что для её решения уже недостаточно существующих физических теорий .
	## Я вижу их хорошо.
	## Я вижу их, хороших.

SELECT $$NGDAIP IF (0C A) (*1C $$NGDAIP BARRIER NPNHA) ; 
	## Практика показывает, что для её решения уже недостаточно существующих физических теорий .

SELECT Gen IF (*-1 GENPREP BARRIER NOTGEN OR V OR Pr) ;
	## Практика показывает, что для её решения уже недостаточно существующих физических теорий .

SELECT Prp IF (*-1 PRPPREP BARRIER NOTPRP OR V OR Pr) ;
	## Экономическая теория не стоит на месте и её развитием в исторической перспективе занимается история экономических учений.

REMOVE A + Fem IF (0  $$NGDAIP) (*1 NounMscNeu + $$NGDAIP BARRIER NPNHA LINK NOT 0 Fem);
REMOVE A + Msc IF (0  $$NGDAIP) (*1 NounFemNeu + $$NGDAIP BARRIER NPNHA LINK NOT 0 Msc);
REMOVE A + Neu IF (0  $$NGDAIP) (*1 NounMscFem + $$NGDAIP BARRIER NPNHA LINK NOT 0 Neu);
	## Большинство населения достаточно хорошо владеют английским языком .
	## Большинство населения говорит на финском языке . 

REMOVE Det + Fem IF (0  $$NGDAIP) (*1 NounMscNeu + $$NGDAIP BARRIER NPNHA LINK NOT 0 Fem);
REMOVE Det + Msc IF (0  $$NGDAIP) (*1 NounFemNeu + $$NGDAIP BARRIER NPNHA LINK NOT 0 Msc);
REMOVE Det + Neu IF (0  $$NGDAIP) (*1 NounMscFem + $$NGDAIP BARRIER NPNHA LINK NOT 0 Neu);
	## В результате наступления советских войск на севере Финляндия объявила о своём выходе из войны .
